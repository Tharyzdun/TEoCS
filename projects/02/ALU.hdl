// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
	// zx :
		Mux16(a=x, b=false, sel=zx, out=zerox);
	// nx :
		Not16(in=zerox, out=notx);
		Mux16(a=zerox, b=notx, sel=nx, out=outnx);
	// zy :
		Mux16(a=y, b=false, sel=zy, out=zeroy);
	// ny :
		Not16(in=zeroy, out=noty);
		Mux16(a=zeroy, b=noty, sel=ny, out=outny);
	//x + y :
		Add16(a=outnx, b=outny, out=xplusy);
	//x & y :
		And16(a=outnx, b=outny, out=xandy);
	// f :	
		Mux16(a=xandy, b=xplusy, sel=f, out=outf);
	// no :	
		Not16(in=outf, out=notoutf);
		Mux16(a=outf, b=notoutf, sel=no, out=tmpout);
		And16(a=tmpout, b=true, out=out);
	// zr :
		Eq16(a=tmpout, b=false, out=outeq1);
		Mux(a=false, b=true, sel=outeq1, out=zr);
	// ng :
		TestNeg16(in=tmpout, out=testneg);
		Mux(a=false, b=true, sel=testneg, out=ng);
}